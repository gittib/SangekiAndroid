plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 33
//    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "work.boardgame.sangeki_rooper"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 14
        versionName "1.2.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        def properties = new Properties()
        properties.load(project.rootProject.file('keystore.properties').newDataInputStream())

        debug {
            storeFile file(properties.getProperty('debug.path'))
            storePassword properties.getProperty('debug.storePassword')
            keyAlias properties.getProperty('debug.alias')
            keyPassword properties.getProperty('debug.keyPassword')
        }

        release {
            storeFile file(properties.getProperty('release.path'))
            storePassword properties.getProperty('release.storePassword')
            keyAlias properties.getProperty('release.alias')
            keyPassword properties.getProperty('release.keyPassword')
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    lintOptions {
        // layout関連の不要な警告をチェック対象外にする
        disable 'ContentDescription', 'RtlHardcoded'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
        }
    }
    flavorDimensions "appli"
    productFlavors {
        sangeki {
            dimension "appli"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Retrofit ////////////////////////////////////////////////////////////////////////////////////
    // picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'
    // gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    // retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    // CallAdapter for RxJava
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    // Explicitly install RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    // for Android
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // Retrofit ここまで /////////////////////////////////////////////////////////////////////////////


    // inline-block的な、よしなに折り返すレイアウト
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // 円形のImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    // 角丸のImageView
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // RoomDatabase ////////////////////////////////////////////////////////////////////////////////
    def room_version = "2.3.0"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // RoomDatabase ここまで ////////////////////////////////////////////////////////////////////////

    // PDFビューワ
    implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'
}